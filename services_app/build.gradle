/**************************************************************************************************
 GRADLE CONFIG
 *************************************************************************************************/

if ((new File(gradle.local)).exists()) {
    apply from: gradle.local
} else {
    apply from: gradle.remote
}

applicationScripts.each {
    apply from: it
}

apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

android {
    testOptions {
        animationsDisabled = true
        unitTests {
            includeAndroidResources = true
        }
    }
    // The following are set in the common.gradle file
    namespace(sevicesNamespace)
    compileSdk(compileVersion)

    defaultConfig {
        minSdkVersion(minVersion)
        targetSdkVersion(targetVersion)
        testInstrumentationRunner(instrumentationRunner)
    }

    buildFeatures {
        aidl true
        buildConfig true
    }

    flavorDimensions.add("stage")

    productFlavors {
        primary {
            dimension 'stage'
            versionCode(primaryVersionCode)
            versionName(primaryVersion)
        }

        snapshot {
            dimension 'stage'
            versionCode(snapshotVersionCode)
            versionName(snapshotVersion)
        }
    }

    signingConfigs {
        release {
            if (shouldSign) {
                storeFile file(signStoreFilePath)
                storePassword signStorePass
                keyAlias signKeyAlias
                keyPassword signKeyPass
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled(minify)
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            if (shouldSign) {
                signingConfig signingConfigs.release
            }
        }
        debug {
            debuggable(true)
            testCoverageEnabled true
            enableUnitTestCoverage true
        }
    }

    lintOptions {
        disable 'InvalidPackage'
    }

    packagingOptions {
        exclude 'META-INF/*'
    }

    compileOptions {
        sourceCompatibility javaVersion
        targetCompatibility javaVersion
    }

    publishing {
        singleVariant("snapshotDebug")
        singleVariant("primaryRelease")
    }
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
    jacoco.excludes = ['jdk.internal.*']
}

configurations.all {
    resolutionStrategy {
        cacheDynamicVersionsFor(0, 'seconds')
        cacheChangingModulesFor(0, 'seconds')
    }
}

allprojects {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
    }
}

dependencies {
    implementation 'joda-time:joda-time:2.10.10'
    implementation 'org.apache.httpcomponents.client5:httpclient5:5.3'
    implementation 'org.apache.httpcomponents.core5:httpcore5:5.2.4'
    implementation 'commons-io:commons-io:2.8.0'
    implementation 'commons-fileupload:commons-fileupload:1.5'
    implementation 'org.opendatakit:odkx-rest-interface:2.2.0-SNAPSHOT'

    implementation 'androidx.annotation:annotation:1.7.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'androidx.fragment:fragment:1.5.7'
    implementation 'com.google.android.material:material:1.7.0'
    implementation 'com.google.firebase:firebase-analytics:19.0.2'
    implementation 'com.google.firebase:firebase-crashlytics:18.2.3'

    implementation "androidx.navigation:navigation-fragment:2.5.3"
    implementation "androidx.navigation:navigation-ui:2.5.3"
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'

    implementation group: 'com.google.zxing', name: 'core', version: '3.3.0'
    implementation group: 'com.journeyapps', name: 'zxing-android-embedded', version: '3.5.0'

    if (libraryProjectPath.exists() && gradle.useLocal) { // Local project is favoured
        implementation project(libraryProjectName)
    } else {
        primaryImplementation(group: odkxGroupId, name: libraryPackageName, version: primaryVersion)
        snapshotImplementation(group: odkxGroupId, name: libraryPackageName, version: snapshotVersion)
    }

    // Testing-only dependencies
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test:rules:1.5.0'

    androidTestImplementation 'androidx.annotation:annotation:1.7.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.5.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'org.mockito:mockito-core:2.19.0'
    androidTestImplementation 'com.google.truth:truth:1.1.3'
    androidTestImplementation 'androidx.test:core:1.5.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.5.1'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:2.19.0'
    testImplementation 'androidx.annotation:annotation:1.7.1'
    testImplementation 'androidx.test:core:1.5.0'
    testImplementation 'org.robolectric:robolectric:4.8.1'
    testImplementation 'android.arch.core:core-testing:1.1.1'
}

task grantPermissionForODKXApp {
    dependsOn grantServicesReadExternalStoragePermission
    dependsOn grantServicesWriteExternalStoragePermission
}


task jacocoTestReport(type: JacocoReport, dependsOn: ['testSnapshotDebugUnitTest']) {
    reports {
        xml.required = true
        html.required = true
        csv.required = false
    }

    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*']
    def debugTree = fileTree(
            dir: "$buildDir/intermediates/classes/snapshotDebug/jacocoSnapshotDebug",
            excludes: fileFilter
    )
    def mainSrc = "$projectDir/src/main/java"

    sourceDirectories.setFrom(files([mainSrc]))
    classDirectories.setFrom(files([debugTree]))
    executionData.setFrom(fileTree(dir: "$buildDir/outputs/unit_test_code_coverage/snapshotDebugUnitTest", includes: [
            "testSnapshotDebugUnitTest.exec"
    ]))

    doFirst {
        def execFiles = fileTree(dir: "$buildDir/outputs/unit_test_code_coverage/snapshotDebugUnitTest", includes: ["testSnapshotDebugUnitTest.exec"]).files
        if (execFiles.empty) {
            throw new GradleException("Jacoco execution data not found. Please run the unit tests before generating the report.")
        } else {
            println "Found Jacoco execution data files: $execFiles"
        }
    }
}



